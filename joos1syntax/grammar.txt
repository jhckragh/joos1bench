Program = [ PackageDeclaration ] { ImportDeclaration } ClassDeclaration .

PackageDeclaration = "package" Name ";" .

ImportDeclaration = "import" Name "." ( Identifier | "*" ) ";" .

ClassDeclaration = "public" [ "final" ] [ "abstract" ] "class" Identifier
                   [ ExtendsDeclaration ] [ ImplementsDeclaration ] ClassBody .

ExtendsDeclaration = "extends" Name .

ImplementsDeclaration = "implements" Name { "," Name } .

ClassBody = "{" { MemberDeclaration } "}" .

MemberDeclaration = MethodDeclaration
                  | ConstructorDeclaration
                  | FieldDeclaration .

(* ***** *)

MethodDeclaration = Access [ "abstract" | "final" | "static" ] TypeExp
                    Identifier FormalParameterList [ ThrowsClause ]
                    ( Block | ";" ) .

ConstructorDeclaration = Access Identifier FormalParameterList
                         [ ThrowsClause ] Block .

FieldDeclaration = Access TypeExp Identifier [ "=" Expression ] ";" .

(* ***** *)

Block = "{" { Statement | LocalDeclarationStatement } "}" .

LocalDeclarationStatement = LocalDeclaration ";" .

LocalDeclaration = TypeExp Identifier [ "=" Expression ] .

Statement = IfStatement
          | WhileStatement
          | ForStatement
          | Block
          | EmptyStatement
          | ExpressionStatement
          | ReturnStatement .

IfStatement = "if" "(" Expression ")" Statement [ "else" Statement ] .

WhileStatement = "while" "(" Expression ")" Statement .

ForStatement = "for" "(" [ ForInitializer ] ";" [ Expression ] ";"
               [ StatementExpression ] ")" Statement .

ForInitializer = StatementExpression | LocalDeclaration .

EmptyStatement = ";" .

ExpressionStatement = StatementExpression ";" .

ReturnStatement = "return" [ Expression ] ";" .

(* ***** *)

Expression = UnaryExpression ( [ "=" Expression ] |
                 { BinaryOperator UnaryExpression } [ "instanceof" Name [ "[" "]" ] ] ) .

StatementExpression = UnaryExpression [ "=" Expression ] .

UnaryExpression = PostfixExpression
                | ( "-" | "!" ) UnaryExpression
                | "(" TypeExp ")" UnaryExpression .

PostfixExpression = PrimaryExpression { PostfixOperator } .

PostfixOperator = "[" Expression "]"
                | "(" [ Expression { "," Expression } ] ")"
                | "." IDENTIFIER .

PrimaryExpression = IDENTIFIER
                  | Literal
                  | "this"
                  | "(" Expression ")"
                  | "new" ( PrimitiveType "[" Expression "]"
                              | Name ( "[" Expression "]" | ArgumentList ) ) .

(* ***** *)

BinaryOperator = "||" | "&&" | "|" | "^" | "&" | "==" | "!=" | "<" | ">"
               | "<=" | ">=" | "+" | "-" | "*" | "/" | "%" .

FormalParameterList = "(" [ FormalParameter { "," FormalParameter } ] ")" .

FormalParameter = TypeExp Identifier .

ArgumentList = "(" [ Expression { "," Expression } ] ")" .

TypeExp = "void" | Type [ "[" "]" ] .

Type = PrimitiveType | Name .

PrimitiveType = "boolean" | "byte" | "char" | "int" | "short" .

Access = "public" | "protected" .

ThrowsClause = "throws" Name { "," Name } .

Name = Identifier { "." Identifier } .

Identifier = IDENTIFIER .

Literal = "null" | BooleanLiteral | IntegerLiteral | CharacterLiteral | StringLiteral .

BooleanLiteral = "true" | "false" .

IntegerLiteral = INTEGER_LITERAL .

CharacterLiteral = CHARACTER_LITERAL .

StringLiteral = STRING_LITERAL .
